import net.fabricmc.loom.task.RemapJarTask

plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('fabric-loom') version('1.3-SNAPSHOT')
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.,

    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }

}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // Minecraft and Fabric mappings
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    // GeckoLib Fabric
    modImplementation('software.bernie.geckolib:geckolib-fabric-1.19:3.1.40')

    // Fabric API
    Set<String> apiModules = [
            'fabric-api-base',
            'fabric-lifecycle-events-v1',
            'fabric-object-builder-api-v1',
            'fabric-rendering-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }
}

processResources {
    inputs.property("version", project.version)
    inputs.property("minecraft_version", project.minecraft_version)
    inputs.property("loader_version", project.loader_version)
    filteringCharset("UTF-8")

    filesMatching("fabric.mod.json") {
        expand("version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version)
    }
}

shadowJar {
    dependencies {
        exclude('net.fabricmc:.*')
        include(project(":common"))
        exclude('/mappings/*')
    }

    archiveFileName = "XPAnimals-${version}-fabric-${minecraft_version}-dev.jar"
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn(tasks.shadowJar)
    input.set(tasks.shadowJar.archiveFile)
    addNestedDependencies.set(true)

    destinationDirectory = file('../target')
    archiveFileName = "XPAnimals-${version}-fabric-${minecraft_version}.jar"
}
remappedShadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

tasks.assemble.dependsOn(tasks.remappedShadowJar)

artifacts {
    archives(remappedShadowJar)
    archives(shadowJar)
}
